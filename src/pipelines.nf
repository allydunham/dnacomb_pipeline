#!/usr/bin/env nextflow
// General processes and workflows to share across NF pipelines
nextflow.enable.dsl = 2
nextflow.enable.strict = true

// All rules taking/emitting reads assume an input channel using the NF-Core structure.
// It contains a tuple [meta, reads] where meta is [id: str, single_end: bool] and
// reads is either [r1, r2] or [r1]



process fastqc {
    tag "$meta.id"

    input:
    tuple val(meta), path(reads)

    output:
    tuple val(meta), path("*.html"), emit: html
    tuple val(meta), path("*.zip") , emit: zip

    script:
    if (meta.single_end) {
        """
        base="${meta.id}_${meta.label}"
        
        case "${reads}" in
            *.fastq.gz) link="\${base}.fastq.gz" ;;
            *.fq.gz)    link="\${base}.fq.gz" ;;
            *.fastq)    link="\${base}.fastq" ;;
            *.fq)       link="\${base}.fq" ;;
            *.fasta.gz) link="\${base}.fasta.gz" ;;
            *.fa.gz)    link="\${base}.fa.gz" ;;
            *.fasta)    link="\${base}.fasta" ;;
            *.fa)       link="\${base}.fa" ;;
            *)          link="\${base}" ;;
        esac

        [ ! -f "\$link" ] && ln -s $reads "\$link"
        fastqc --threads $task.cpus "\$link"
        """
    } else {
        """
        f="${meta.id}_f_${meta.label}"
        r="${meta.id}_r_${meta.label}"
        
        case "${reads[0]}" in
            *.fastq.gz) f="\${f}.fastq.gz" ;;
            *.fq.gz)    f="\${f}.fq.gz" ;;
            *.fastq)    f="\${f}.fastq" ;;
            *.fq)       f="\${f}.fq" ;;
            *.fasta.gz) f="\${f}.fasta.gz" ;;
            *.fa.gz)    f="\${f}.fa.gz" ;;
            *.fasta)    f="\${f}.fasta" ;;
            *.fa)       f="\${f}.fa" ;;
            *)          f="\${f}" ;;
        esac
      
        case "${reads[1]}" in
            *.fastq.gz) r="\${r}.fastq.gz" ;;
            *.fq.gz)    r="\${r}.fq.gz" ;;
            *.fastq)    r="\${r}.fastq" ;;
            *.fq)       r="\${r}.fq" ;;
            *.fasta.gz) r="\${r}.fasta.gz" ;;
            *.fa.gz)    r="\${r}.fa.gz" ;;
            *.fasta)    r="\${r}.fasta" ;;
            *.fa)       r="\${r}.fa" ;;
            *)          r="\${r}" ;;
        esac

        [ ! -f "\$f" ] && ln -s ${reads[0]} "\$f"
        [ ! -f "\$r" ] && ln -s ${reads[1]} "\$r"
        fastqc --threads $task.cpus "\$f" "\$r"
        """
    }

    stub:
    if (meta.single_end) {
        """
        touch ${meta.id}_${meta.label}.zip ${meta.id}_${meta.label}.html
        """
    } else {
        """
        touch ${meta.id}_f_${meta.label}.zip ${meta.id}_r_${meta.label}.zip
        touch ${meta.id}_f_${meta.label}.html ${meta.id}_r_${meta.label}.html
        """
    }
}

process multiqc {
    tag "multiqc"

    input:
    path zip_files
    path config

    output:
    path "multiqc_report.html"       , emit: report
    path "multiqc_report.zip"        , emit: zip
    path "multiqc_report_data/*"     , emit: data
    path "multiqc_report_plots/*"    , optional:true, emit: plots

    script:
    """
    multiqc -c ${config} .
    zip -r multiqc_report.zip multiqc_report.html multiqc_report_data multiqc_report_plots
    """

    stub:
    """
    mkdir multiqc_report_data
    touch multiqc_report.html multiqc_report_data/multiqc_data.json multiqc_report.zip
    """
}

process seqkit_stats {
    tag "seqkit stats"

    input:
    path reads

    output:
    path "seqkit_stats.tsv", emit: tsv

    script:
    """
    seqkit stats * > seqkit_stats.tsv
    """

    stub:
    """
    touch seqkit_stats.tsv
    """
}

process seqtk {
    tag "$meta.id"

    input:
    tuple val(meta), path(reads)
    val sample_size
    val seed

    output:
    tuple val(meta), path("*_downsampled.fq.gz"), emit: reads

    script:
    if (meta.single_end) {
        """
        seqtk sample -s${seed} ${reads[0]} ${sample_size} | gzip --no-name > ${meta.id}_downsampled.fq.gz
        """
    } else {
        """
        seqtk sample -s${seed} ${reads[0]} ${sample_size} | gzip --no-name > ${meta.id}_f_downsampled.fq.gz
        seqtk sample -s${seed} ${reads[1]} ${sample_size} | gzip --no-name > ${meta.id}_r_downsampled.fq.gz
        """
    }

    stub:
    if (meta.single_end) {
        """
        touch ${meta.id}_downsampled.fq.gz
        """
    } else {
        """
        touch ${meta.id}_f_downsampled.fq.gz ${meta.id}_r_downsampled.fq.gz
        """
    }
}

process pear {
    tag "$meta.id"

    input:
    tuple val(meta), path(reads)
    val args

    output:
    tuple val(meta), path("*_merged.assembled.fastq.gz"), emit: assembled
    tuple val(meta), path("*_merged.unassembled.*.fastq.gz"), emit: unassembled
    tuple val(meta), path("*_merged.discarded.fastq.gz"), emit: discarded

    script:
    if (meta.single_end) {
        error("Trying to merge single end reads")
    } else {
        """
        gunzip -f ${reads[0]}
        gunzip -f ${reads[1]}
        pear  --threads ${task.cpus} -f ${reads[0].baseName} -r ${reads[1].baseName} -o ${meta.id}_merged -j $task.cpus $args
        gzip -f ${meta.id}_merged.assembled.fastq
        gzip -f ${meta.id}_merged.unassembled.forward.fastq
        gzip -f ${meta.id}_merged.unassembled.reverse.fastq
        gzip -f ${meta.id}_merged.discarded.fastq
        """
    }

    stub:
    if (meta.single_end) {
        error("Trying to merge single end reads")
    } else {
        """
        touch \\
           ${meta.id}_merged.assembled.fastq.gz \\
           ${meta.id}_merged.unassembled.forward.fastq.gz \\
           ${meta.id}_merged.unassembled.reverse.fastq.gz \\
           ${meta.id}_merged.discarded.fastq.gz
        """
    }
}

process cutadapt {
    tag "$meta.id"

    input:
    tuple val(meta), path(reads)
    val args

    output:
    tuple val(meta), path('*{,_f,_r}_trimmed.fastq.gz'), emit: reads
    tuple val(meta), path('*{,_f,_r}_untrimmed.fastq.gz'), emit: untrimmed_reads, optional: true
    tuple val(meta), path('*.log'), emit: log
    tuple val(meta), path('*.json'), emit: json

    script:
    if (meta.single_end) {
        """
        cutadapt \\
          --cores $task.cpus \\
          --json=${meta.id}.cutadapt.json \\
          $args \\
          -o ${meta.id}_trimmed.fastq.gz \\
          --untrimmed-output ${meta.id}_untrimmed.fastq.gz \\
          $reads \\
        > ${meta.id}.cutadapt.log
        """
    } else {
        """
        cutadapt \\
          --cores $task.cpus \\
          --json=${meta.id}.cutadapt.json \\
          $args \\
          -o ${meta.id}_f_trimmed.fastq.gz \\
          -p ${meta.id}_r_trimmed.fastq.gz \\
          --untrimmed-output ${meta.id}_f_untrimmed.fastq.gz \\
          --untrimmed-paired-output ${meta.id}_r_untrimmed.fastq.gz \\
          $reads \\
        > ${meta.id}.cutadapt.log
        """
    }

    stub:
    if (meta.single_end) {
        """
        touch \\
          ${meta.id}_trimmed.fastq.gz \\
          ${meta.id}_untrimmed.fastq.gz \\
          ${meta.id}.cutadapt.log \\
          ${meta.id}.cutadapt.json
        """
    } else {
        """
        touch \\
          ${meta.id}_f_trimmed.fastq.gz \\
          ${meta.id}_r_trimmed.fastq.gz \\
          ${meta.id}_f_untrimmed.fastq.gz \\
          ${meta.id}_r_untrimmed.fastq.gz \\
          ${meta.id}.cutadapt.log \\
          ${meta.id}.cutadapt.json
        """
    }
}

process dnacomb {
    tag "$meta.id"

    input:
    tuple val(meta), path(reads)
    path libspec
    path library
    val args

    output:
    tuple val(meta), path("*.counts.tsv"), emit: counts
    tuple val(meta), path("*.library_counts.tsv"), emit: library_counts, optional: true
    tuple val(meta), path("*.summary.tsv"), emit: summary
    tuple val(meta), path("*.filtered.tsv"), emit: filtered
    tuple val(meta), path("*.log"), emit: log

    script:
    ls = libspec ? "--library-spec ${libspec}" : ""
    lib_paths = library instanceof List ? library.join(" ") : library
    lib = library ? "--library ${lib_paths}" : ""
    r2 = !meta.single_end ? reads[1] : ""
    cmdargs = "--verbose ${ls} ${lib} --threads ${task.cpus} --output ${meta.id} ${args}"
    """
    dnacomb ${cmdargs} ${reads[0]} ${r2} > ${meta.id}.log 2>&1
    """

    stub:
    """
    touch \\
       ${meta.id}.counts.tsv \\
       ${meta.id}.library_counts.tsv \\
       ${meta.id}.summary.tsv \\
       ${meta.id}.filtered.tsv \\
       ${meta.id}.log
    """
}

process count_records {
    tag "${meta.id}:${meta.label}"

    input:
    tuple val(meta), path(reads)

    output:
    path "${meta.id}_${meta.stage}_${meta.label}.tsv", emit: rows

    script:
    """
    count_seqs.py \
      --sample '${meta.id}' \
      --stage '${meta.stage}' \
      --label '${meta.label}' \
      ${meta.single_end ? '' : '--paired'} \
      ${reads} \
      > ${meta.id}_${meta.stage}_${meta.label}.tsv
    """

    stub:
    """
    printf '${meta.id}\\t${meta.stage}\\t${meta.label}\\tsingle\\tfile.fa\\tfasta\\t100\\n' > ${meta.id}_${meta.stage}_${meta.label}.tsv
    """
}

process combine_record_counts {
    input:
    path rows

    output:
    path "record_counts.tsv", emit: tsv

    script:
    """
    printf 'sample_id\\tstage\\tlabel\\tread\\tfile\\tformat\\trecords\\n' \
      > record_counts.tsv
    cat ${rows} >> record_counts.tsv
    """
}

process qc_counts {
    input:
    path dnacomb_output
    path libraries
    path counts
    path template
    val roots

    output:
    path "dnacomb_qc_report.html", emit: qc_html

    script:
    """
    render_qc_report.R \
        --roots ${roots} \
        --libraries ${libraries} \
        --counts ${counts} \
        --rmd ${template} \
        --output dnacomb_qc_report
    """

    stub:
    """
    touch dnacomb_qc_report.html
    """
}
